#!/usr/bin/perl
use strict;
use Data::Dumper;

=head aa

     1	_offset 20
     2	1 POINTS, LINES, PLANES, AND ANGLES
     3	    Some Basic Figures
     4	        1-1 A Game and Some Geometry 1
     5	        1-2 Points, Lines, and Planes 5
     6	    Definitions and Postulates
     7	        1-3 Segments, Rays, and Distance 11
     8	        1-4 Angles 17
     9	        1-5 Postulates and Theorems Relating Points, Lines, and Planes 22

to
BookmarkBegin
BookmarkTitle: 1 POINTS, LINES, PLANES, AND ANGLES
BookmarkLevel: 1
BookmarkPageNumber: 21

BookmarkBegin
BookmarkTitle: Some Basic Figures
BookmarkLevel: 2
BookmarkPageNumber: 21

BookmarkBegin
BookmarkTitle: 1-1 A Game and Some Geometry 1
BookmarkLevel: 3
BookmarkPageNumber: 22

BookmarkBegin
BookmarkTitle: 1-2 Points, Lines, and Planes 5
BookmarkLevel: 3
BookmarkPageNumber: 25

=cut

sub main
{
    if (scalar @ARGV == 0) {
        print("$0 {index.txt file}\n");
        exit(1);
    }
    open F, "<$ARGV[0]" or die;
    my @lines = <F>;
    close F;
    #checkformat(\@lines);
    @lines = map {s/\s*$//s; $_} @lines;   # remove end spaces
    @lines = grep {$_ !~ /^\s*$/} @lines;  # remove space-only lines

    my $offset = 0;
    if ($lines[0] =~ m{^_offset\s+(\d+)$}) {
        $offset = $1;
        shift @lines;
    }
    my $bookmarks = mkbookmark(\@lines, $offset);
    for my $line (@$bookmarks) {
        print("$line\n");
    }
}

sub mkbookmark
{
    my ($lines, $offset) = @_;
    my $bookmark = [];
    for my $line (@$lines) {
        my $offset = $offset;
        my ($space, $title, $page) = ('','','','');
        if ($line =~ m{^(\s*)(.+)\s+(\d+)$}) {
            ($space, $title, $page) = ($1, $2, $3);
            $title = "$title $page";
        }
        elsif ($line =~ m{^(\s*)(.+)\s+(\d+)\*$}) {
            ($space, $title, $page) = ($1, $2, $3);
            $offset = 0;
        }
        elsif ($line =~ m{^(\s*)(.+)$}) {
            ($space, $title) = ($1, $2);
            $page = 1;
        }
        $page += $offset;
        my $level = int(length($space) / 4) + 1;
        $title =~ s{'}{\&apos;}g;
        push @$bookmark, 'BookmarkBegin';
        push @$bookmark, "BookmarkTitle: $title";
        push @$bookmark, "BookmarkLevel: $level";
        push @$bookmark, "BookmarkPageNumber: $page";
    }

    return $bookmark;
}

main();
